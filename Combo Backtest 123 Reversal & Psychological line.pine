Script Name: Combo Backtest 123 Reversal & Psychological line
Author: HPotter
Description: This is combo strategies for get a cumulative signal.

 First strategy
 This System was created from the Book "How I Tripled My Money In The
 Futures Market" by Ulf Jensen, Page 183. This is reverse type of strategies.
 The strategy buys at market, if close price is higher than the previous close
 during 2 days and the meaning of 9-days Stochastic Slow...
PineScript code:

Pine Scriptâ„¢ strategy
Combo Backtest 123 Reversal & Psychological line
//@version=4
////////////////////////////////////////////////////////////
//    Copyright  by  HPotter  v1.0  30/04/2021
//  This  is  combo  strategies  for  get  a  cumulative  signal.
//
//  First  strategy
//  This  System  was  created  from  the  Book  "How  I  Tripled  My  Money  In  The
//  Futures  Market"  by  Ulf  Jensen,  Page  183.  This  is  reverse  type  of  strategies.
//  The  strategy  buys  at  market,  if  close  price  is  higher  than  the  previous  close
//  during  2  days  and  the  meaning  of  9-days  Stochastic  Slow  Oscillator  is  lower  than  50.
//  The  strategy  sells  at  market,  if  close  price  is  lower  than  the  previous  close  price
//  during  2  days  and  the  meaning  of  9-days  Stochastic  Fast  Oscillator  is  higher  than  50.
//
//  Second  strategy
//  Psychological  line  (PSY),  as  an  indicator,  is  the  ratio  of  the  number  of
//  rising  periods  over  the  total  number  of  periods.  It  reflects  the  buying
//  power  in  relation  to  the  selling  power.
//  If  PSY  is  above  50%,  it  indicates  that  buyers  are  in  control.  Likewise,
//  if  it  is  below  50%,  it  indicates  the  sellers  are  in  control.  If  the  PSY
//  moves  along  the  50%  area,  it  indicates  balance  between  the  buyers  and
//  sellers  and  therefore  there  is  no  direction  movement  for  the  market.
//
//  WARNING:
//  -  For  purpose  educate  only
//  -  This  script  to  change  bars  colors.
////////////////////////////////////////////////////////////
Reversal123(Length,  KSmoothing,  DLength,  Level)  =>
        vFast  =  sma(stoch(close,  high,  low,  Length),  KSmoothing)
        vSlow  =  sma(vFast,  DLength)
        pos  =  0.0
        pos  :=  iff(close[2]  <  close[1]  and  close  >  close[1]  and  vFast  <  vSlow  and  vFast  >  Level,  1,
                          iff(close[2]  >  close[1]  and  close  <  close[1]  and  vFast  >  vSlow  and  vFast  <  Level,  -1,  nz(pos[1],  0)))
        pos
PLine(Length)  =>
        pos  =  0.0
        cof  =  close  >  close[1]?  1:0
        xPSY  =  sum(cof,Length)  /  Length  *  100
        pos:=  iff(xPSY  >  50,  1,
                      iff(xPSY  <  50,  -1,  nz(pos[1],  0)))
        pos
strategy(title="Combo  Backtest  123  Reversal  &  Psychological  line",  shorttitle="Combo",  overlay  =  true)
line1  =  input(true,  "----  123  Reversal  ----")
Length  =  input(14,  minval=1)
KSmoothing  =  input(1,  minval=1)
DLength  =  input(3,  minval=1)
Level  =  input(50,  minval=1)
//-------------------------
line2  =  input(true,  "----  Psychological  line  ----")
LengthPLine  =  input(20,  minval=1)
reverse  =  input(false,  title="Trade  reverse")
posReversal123  =  Reversal123(Length,  KSmoothing,  DLength,  Level)
posPLine  =  PLine(LengthPLine)
pos  =  iff(posReversal123  ==  1  and  posPLine  ==  1  ,  1,
              iff(posReversal123  ==  -1  and  posPLine  ==  -1,  -1,  0))
possig  =  iff(reverse  and  pos  ==  1,  -1,
                    iff(reverse  and  pos  ==  -1  ,  1,  pos))
if  (possig  ==  1  )
        strategy.entry("Long",  strategy.long)
if  (possig  ==  -1  )
        strategy.entry("Short",  strategy.short)
if  (possig  ==  0)
        strategy.close_all()
barcolor(possig  ==  -1  ?  #b50404:  possig  ==  1  ?  #079605  :  #0536b3  )
Expand (66 lines)
