Script Name: Combo Backtest 123 Reversal & Smoothed RSI
Author: HPotter
Description: This is combo strategies for get a cumulative signal.

 First strategy
 This System was created from the Book "How I Tripled My Money In The
 Futures Market" by Ulf Jensen, Page 183. This is reverse type of strategies.
 The strategy buys at market, if close price is higher than the previous close
 during 2 days and the meaning of 9-days Stochastic Slow...
PineScript code:

Pine Scriptâ„¢ strategy
Combo Backtest 123 Reversal & Smoothed RSI
//@version=4
////////////////////////////////////////////////////////////
//    Copyright  by  HPotter  v1.0  20/07/2021
//  This  is  combo  strategies  for  get  a  cumulative  signal.
//
//  First  strategy
//  This  System  was  created  from  the  Book  "How  I  Tripled  My  Money  In  The
//  Futures  Market"  by  Ulf  Jensen,  Page  183.  This  is  reverse  type  of  strategies.
//  The  strategy  buys  at  market,  if  close  price  is  higher  than  the  previous  close
//  during  2  days  and  the  meaning  of  9-days  Stochastic  Slow  Oscillator  is  lower  than  50.
//  The  strategy  sells  at  market,  if  close  price  is  lower  than  the  previous  close  price
//  during  2  days  and  the  meaning  of  9-days  Stochastic  Fast  Oscillator  is  higher  than  50.
//
//  Second  strategy
//  This  is  new  version  of  RSI  oscillator  indicator,  developed  by  John  Ehlers.
//  The  main  advantage  of  his  way  of  enhancing  the  RSI  indicator  is  smoothing
//  with  minimum  of  lag  penalty.
//
//  WARNING:
//  -  For  purpose  educate  only
//  -  This  script  to  change  bars  colors.
////////////////////////////////////////////////////////////
Reversal123(Length,  KSmoothing,  DLength,  Level)  =>
        vFast  =  sma(stoch(close,  high,  low,  Length),  KSmoothing)
        vSlow  =  sma(vFast,  DLength)
        pos  =  0.0
        pos  :=  iff(close[2]  <  close[1]  and  close  >  close[1]  and  vFast  <  vSlow  and  vFast  >  Level,  1,
                          iff(close[2]  >  close[1]  and  close  <  close[1]  and  vFast  >  vSlow  and  vFast  <  Level,  -1,  nz(pos[1],  0)))
        pos
SRSI(Length,  TopBand,LowBand)  =>
        pos  =  0.0
        xValue  =  (close  +  2  *  close[1]  +  2  *  close[2]  +  close[3]  )  /  6
        CU23  =  sum(iff(xValue  >  xValue[1],  xValue  -  xValue[1],  0),  Length)
        CD23  =  sum(iff(xValue  <  xValue[1],  xValue[1]  -  xValue,  0),  Length)
        nRes  =  iff(CU23  +  CD23  !=  0,  CU23/(CU23  +  CD23),  0)
        pos:=  iff(nRes  >  TopBand,  1,
                      iff(nRes  <  LowBand,  -1,  nz(pos[1],  0)))
        pos
strategy(title="Combo  Backtest  123  Reversal  &  Smoothed  RSI",  shorttitle="Combo",  overlay  =  true)
line1  =  input(true,  "----  123  Reversal  ----")
Length  =  input(14,  minval=1)
KSmoothing  =  input(1,  minval=1)
DLength  =  input(3,  minval=1)
Level  =  input(50,  minval=1)
//-------------------------
line2  =  input(true,  "----  Smoothed  RSI  ----")
LengthRSI  =  input(10,  minval=1)
TopBand  =  input(0.8,  step=0.01)
LowBand  =  input(0.2,  step=0.01)
reverse  =  input(false,  title="Trade  reverse")
posReversal123  =  Reversal123(Length,  KSmoothing,  DLength,  Level)
posSRSI  =  SRSI(LengthRSI,  TopBand,LowBand  )
pos  =  iff(posReversal123  ==  1  and  posSRSI  ==  1  ,  1,
              iff(posReversal123  ==  -1  and  posSRSI  ==  -1,  -1,  0))
possig  =  iff(reverse  and  pos  ==  1,  -1,
                    iff(reverse  and  pos  ==  -1  ,  1,  pos))
if  (possig  ==  1  )
        strategy.entry("Long",  strategy.long)
if  (possig  ==  -1  )
        strategy.entry("Short",  strategy.short)
if  (possig  ==  0)
        strategy.close_all()
barcolor(possig  ==  -1  ?  #b50404:  possig  ==  1  ?  #079605  :  #0536b3  )
Expand (66 lines)
